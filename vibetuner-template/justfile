import 'base.justfile'

VERSION := `uv run dunamai from git 2>/dev/null || echo 0.0.0`
PROJECT_SLUG := `uv run python -c "import yaml; print(yaml.safe_load(open('.copier-answers.yml'))['project_slug'].strip())"`
FQDN := `uv run python -c "import yaml; print(yaml.safe_load(open('.copier-answers.yml')).get('fqdn', '').strip())"`
ENABLE_WATCHTOWER := `uv run python -c "import yaml; print(str(yaml.safe_load(open('.copier-answers.yml')).get('enable_watchtower', False)).lower())"`
PYTHON_VERSION := `tr -d '\n\r' < .python-version`

# Default: update dependencies
default: update-deps

# Runs the dev environment with watch mode and cleans up orphans
[group('Local Development')]
dev: install-deps
    ENVIRONMENT=development \
    COMPOSE_BAKE=true \
    PYTHON_VERSION={{ PYTHON_VERSION }} \
    COMPOSE_PROJECT_NAME={{ PROJECT_SLUG }} \
    docker compose -f compose.dev.yml up --watch --remove-orphans

# Runs the dev environment locally without Docker
[group('Local Development')]
local-dev PORT="8000": install-deps
    @vibetuner run dev --port {{ PORT }}

# Runs the task worker locally without Docker
[group('Local Development')]
worker-dev: install-deps
    @vibetuner run dev worker

# Builds the dev image with COMPOSE_BAKE set
[group('CI/CD')]
build-dev: install-deps
    ENVIRONMENT=development \
    COMPOSE_BAKE=true \
    PYTHON_VERSION={{ PYTHON_VERSION }} \
    COMPOSE_PROJECT_NAME={{ PROJECT_SLUG }} \
    docker compose --progress=plain -f compose.dev.yml build

# Builds the prod image with COMPOSE_BAKE set (only if on a clean, tagged commit)
[group('CI/CD')]
test-build-prod: install-deps
    ENVIRONMENT=production \
    PYTHON_VERSION={{ PYTHON_VERSION }} \
    COMPOSE_PROJECT_NAME={{ PROJECT_SLUG }} \
    FQDN={{ FQDN }} \
    ENABLE_WATCHTOWER={{ ENABLE_WATCHTOWER }} \
    docker buildx bake -f compose.prod.yml

# Builds the prod image with COMPOSE_BAKE set (only if on a clean, tagged commit)
[group('CI/CD')]
build-prod: _check-clean _check-last-commit-tagged install-deps
    ENVIRONMENT=production \
    VERSION={{ VERSION }} \
    PYTHON_VERSION={{ PYTHON_VERSION }} \
    COMPOSE_PROJECT_NAME={{ PROJECT_SLUG }} \
    FQDN={{ FQDN }} \
    ENABLE_WATCHTOWER={{ ENABLE_WATCHTOWER }} \
    docker buildx bake -f compose.prod.yml

# Builds the prod image with COMPOSE_BAKE set (only if on a clean, tagged commit)
[group('CI/CD')]
release: _check-clean _check-last-commit-tagged install-deps
    ENVIRONMENT=production \
    VERSION={{ VERSION }} \
    PYTHON_VERSION={{ PYTHON_VERSION }} \
    COMPOSE_PROJECT_NAME={{ PROJECT_SLUG }} \
    FQDN={{ FQDN }} \
    ENABLE_WATCHTOWER={{ ENABLE_WATCHTOWER }} \
    docker buildx bake -f compose.prod.yml --push

# Builds the prod image with COMPOSE_BAKE set (only if on a clean, tagged commit)
[group('CI/CD')]
deploy-latest HOST: release
    DOCKER_HOST="ssh://{{ HOST }}" \
    PYTHON_VERSION={{ PYTHON_VERSION }} \
    COMPOSE_PROJECT_NAME={{ PROJECT_SLUG }} \
    FQDN={{ FQDN }} \
    ENABLE_WATCHTOWER={{ ENABLE_WATCHTOWER }} \
    docker compose -f compose.prod.yml up -d --remove-orphans --pull always

# Run all linting checks
[group('linting')]
lint: lint-md lint-py type-check lint-toml

# Format all code
[group('formatting')]
format: format-py format-toml

# Complete i18n workflow: extract, update, and compile translations
[group('localization')]
i18n: extract-translations update-locale-files compile-locales
    @echo "âœ“ Complete i18n workflow finished"

# Generate or update README.md using Claude Code
[group('documentation')]
readme:
    @echo "ðŸ“ Generating README.md..."
    @claude /readme
    @echo "âœ“ README.md generated/updated"

# Extracts translations from source files
[group('localization')]
extract-translations:
    @uv run pybabel extract -F babel.cfg -o locales/messages.pot .

# Creates a new language file for localization
[group('localization')]
new-locale LANG:
    @uv run pybabel init -i locales/messages.pot -d locales -l {{ LANG }}

# Updates existing language files for localization
[group('localization')]
update-locale-files:
    @find locales -type f -path "*/LC_MESSAGES/messages.po" -exec sh -c 'echo " â†º {}"; msguniq "{}" -o "{}"; msgmerge --update --backup=none --previous "{}" locales/messages.pot' \;

# Compiles the language files into binary format
[group('localization')]
compile-locales:
    @uv run pybabel compile -d locales

# Dump untranslated strings per language to a given DEST directory
[group('localization')]
dump-untranslated DEST:
    #!/usr/bin/env bash

    mkdir -p {{ DEST }}

    for LANG_DIR in locales/??; do
        LANG=$(basename ${LANG_DIR} | cut -d/ -f1)
        msgattrib --untranslated ./locales/${LANG}/LC_MESSAGES/messages.po > "{{ DEST }}/untranslated_${LANG}.po"
    done

# Update to the latest version of the project scaffolding
[group('scaffolding')]
update-scaffolding:
    @echo "Updating project scaffolding..."
    @copier update -A --trust
    @bun install
    @uv sync --all-extras
    @echo "Project scaffolding updated."
    @echo "Please review the changes and commit."

# Initialize git repo
[group('initialization')]
git-init PROJECT: install-deps
    @[ -d .git ] || (git init && git add . && SKIP=no-commit-to-branch git commit -m "initial commit for {{ PROJECT }}" && git tag -a "v0.0.1" -m "Initial version")

[group('initialization')]
create-github-repo REPO:
    @gh repo create {{ REPO }} --private -s . --push
